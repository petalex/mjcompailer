
package rs.ac.bg.etf.pp1;

import java_cup.runtime.*;
import org.apache.log4j.*;
import rs.ac.bg.etf.pp1.ast.*;

parser code {:
	Logger logger;
	
	boolean errorDetected;
	
	public boolean isErrorDetected() {
		return errorDetected;
	}
   	
    public void report_fatal_error(String message, Object info) throws java.lang.Exception {
      done_parsing();
      report_error(message, info);
    }
  
    public void syntax_error(Symbol cur_token) {
        report_error("Syntax error", cur_token);
    }
  
    public void unrecovered_syntax_error(Symbol cur_token) throws java.lang.Exception {
        report_fatal_error("Unrecovered syntax error", cur_token);
    }

    public void report_error(String message, Object info) {
    	errorDetected = true;
    	StringBuilder stringBuilder = new StringBuilder("[Parser] ");
    	stringBuilder.append(message);
    	if (info instanceof Symbol) {
            stringBuilder.append(" (line: ").append(((Symbol)info).left).append(")");
        }
        logger.error(stringBuilder.toString());
    }
:}

init with {:
	logger = Logger.getLogger(getClass());
	errorDetected = false;
:}

scan with {:
	Symbol symbol = this.getScanner().next_token();
	if (symbol != null && symbol.value != null) {
		logger.info("[Lexer] Token " + symbol.toString() + ": " + symbol.value.toString());
	}
	return symbol;
:}


terminal PROGRAM, BREAK, CLASS, INTERFACE, ENUM, ELSE, CONST, IF, NEW, PRINT, READ, RETURN, VOID, FOR, EXTENDS, IMPLEMENTS, CONTINUE;
terminal PUBLIC, PROTECTED, PRIVATE;
terminal String IDENT;
terminal Character CHAR;
terminal Boolean BOOL;
terminal Integer NUM;
terminal ADD, SUB, MUL, DIV, MOD, EQU, NEQ, GRT, GTE, LST, LTE, AND, OR;
terminal ASSIGN, INC, DEC, SEMICOLON, COMMA, PERIOD, LPARENT, RPARENT, LSQUARE, RSQUARE, LCURLY, RCURLY;

nonterminal Program, ProgName, DeclList, MethodDeclList, Decl, ConstDecl, VarDecl, ClassDecl, EnumDecl, InterfaceDecl;
nonterminal ConstType, ConstIdentList, ConstIdent, EnumName, EnumIdentList, EnumIdent, FieldDecl, FieldDeclEnd;
nonterminal VarIdent, LastVarIdent, VarType, VarIdentList, ExtndsImplmnts, ExtendsImplements, Extends, Implements;
nonterminal TypeList, VarDeclList, FieldDeclList, ClassMethodDeclBlock, ClassMethodDeclList, MethodDecl;
nonterminal MethType, MethDeclEnd, ClassMethodDecl, IntrfaceName, InterfaceMethType, InterfaceMethName;
nonterminal InterfaceMethodDeclList, InterfaceMethodDecl, FormParams, FormParamList, FormParam, LastFormParam;
nonterminal StatementList, Statement, DesignatorStmt, AssignDesignatorStmt, IfCond, ForDesignatorStmt;
nonterminal LastForDesignatorStmt, LastForAssignDesignatorStmt, ForDeclEnd, ForCond, ActualParams;
nonterminal ActualParamExprList, AssignOp, RelOp, AddOp, MulOp, InitList, InitExprList;
nonterminal ExtndsImplmntsEnd, FldModifier, FldType, FieldIdentList, FieldIdent, LastFieldIdent;
nonterminal ClassMethModifier, ClassMethType, ClassMethDeclEnd, Modifier;

nonterminal NewInitArrExprEnd, IfStmtEnd, ElseStmtEnd;

nonterminal rs.etf.pp1.symboltable.concepts.Obj Designator;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Type, Expression, Term, Factor;
nonterminal rs.etf.pp1.symboltable.concepts.Struct Condition, CondTerm, CondFact;

nonterminal rs.etf.pp1.symboltable.concepts.Obj MethName, ClassMethName, ClssName;

precedence left ELSE;

Program ::= (Program) PROGRAM ProgName DeclList LCURLY MethodDeclList RCURLY;

ProgName ::= (ProgramName) IDENT:name;

DeclList ::= (Declarations) DeclList Decl |
			 (NoDeclarations) /* Epsilon */
			 ;

Decl ::= (ConstDeclaration) ConstDecl | 
		 (VarDeclaration) VarDecl | 
		 (ClassDeclaration) ClassDecl | 
		 (EnumDeclaration) EnumDecl | 
		 (InterfaceDeclaration) InterfaceDecl
		 ;

ConstDecl ::= (ConstDeclDefinition) CONST ConstType ConstIdentList SEMICOLON;

ConstType ::= (ConstantType) Type;

ConstIdentList ::= (ConstIdentifiers) ConstIdentList COMMA ConstIdent |
				   (FirstConstIdentifier) ConstIdent
				   ;

ConstIdent ::= (NumConstIdentifier) IDENT:name ASSIGN NUM:value |
			   (CharConstIdentifier) IDENT:name ASSIGN CHAR:value |
			   (BoolConstIdentifier) IDENT:name ASSIGN BOOL:value
			   ;
			  
EnumDecl ::= (EnumDeclDefinition) ENUM EnumName LCURLY EnumIdentList RCURLY;

EnumName ::= (EnumerationName) IDENT:name;

EnumIdentList ::= (EnumIdentifiers) EnumIdentList COMMA EnumIdent |
				  (FirstEnumIdentifier) EnumIdent
				  ;

EnumIdent ::= (NumEnumIdentifier) IDENT:name ASSIGN NUM:value |
			  (EnumIdentifier) IDENT:name
			  ;

VarDecl ::= (VarDeclDefinition) VarType VarIdentList;

VarType ::= (VariableType) Type;

Modifier ::= (PublicModifier) PUBLIC |
			 (ProtectedModifier) PROTECTED |
			 (PrivateModifier) PRIVATE
			 ;

FieldDecl ::= (FieldDeclDefinition) FldModifier FldType FieldIdentList;

FldModifier ::= (FieldModifer) Modifier;
			 
FldType ::= (FieldType) Type;

FieldIdentList ::= (FieldIdentifiers) FieldIdent FieldIdentList |
				   (LastFieldIdentifer) LastFieldIdent
				   ;
				   
FieldIdent ::= (ArrayFieldIdentifier) IDENT:name LSQUARE RSQUARE COMMA |
			   (FieldIdentifier) IDENT:name COMMA |
			   (FieldCommaError) error COMMA:c
	 	  	   {: parser.report_error("Syntax error recovery: Field declaration[comma] (line: " + cleft + ")", null); :}
			   ;

LastFieldIdent ::= (LastArrayFieldIdentifier) IDENT:name LSQUARE RSQUARE SEMICOLON |
			 	   (LastSingleFieldIdentifier) IDENT:name SEMICOLON |
			 	   (FieldSemicolonError) error SEMICOLON:s
	 			   {: parser.report_error("Syntax error recovery: Field declaration[semicolon] (line: " + sleft + ")", null); :}
			 	   ;

VarIdentList ::= (VarIdentifiers) VarIdent VarIdentList |
				 (LastVarIdentifier) LastVarIdent
		 	 	 ;

VarIdent ::= (ArrayVarIdentifier) IDENT:name LSQUARE RSQUARE COMMA |
			 (VarIdentifier) IDENT:name COMMA |
			 (GlobalVarCommaError) error COMMA:c
	 	  	 {: parser.report_error("Syntax error recovery: Global variable declaration[comma] (line: " + cleft + ")", null); :}
			 ;

LastVarIdent ::= (LastArrayVarIdentifier) IDENT:name LSQUARE RSQUARE SEMICOLON |
			 	 (LastSingleVarIdentifier) IDENT:name SEMICOLON |
			 	 (GlobalVarSemicolonError) error SEMICOLON:s
	 			 {: parser.report_error("Syntax error recovery: Global variable declaration[semicolon] (line: " + sleft + ")", null); :}
			 	 ;

ClassDecl ::= (ClassDeclDefinition) CLASS ClssName ExtndsImplmnts ExtndsImplmntsEnd FieldDeclList FieldDeclEnd ClassMethodDeclBlock RCURLY;

ClssName ::= (ClassName) IDENT:name;

ExtndsImplmntsEnd ::= (ExtendsAndImplementsEnd) /* Epsilon */;

FieldDeclEnd ::= (ClassFieldDeclarationEnd) /* Epsilon */;

ExtndsImplmnts ::= (ExtendsAndImplementsWrapper) ExtendsImplements TypeList LCURLY |
				   (ExtendsClassWrapper) Extends |
				   (ImplementsInterfacesWrapper) Implements LCURLY |
				   (NoExtendsAndImplements) LCURLY
				   ;
				 	  
ExtendsImplements ::= (ExtendsAndImplements) EXTENDS Type IMPLEMENTS |
					  (ExtendsAndImplementsError) error IMPLEMENTS:i
					  {: parser.report_error("Syntax error recovery: Extending class[implements] (line: " + ileft + ")", null); :}
					  ;

Extends ::= (ExtendsClass) EXTENDS Type LCURLY |
			(ExtendsClassError) error LCURLY:l
			{: parser.report_error("Syntax error recovery: Extending class[left curly bracket] (line: " + lleft + ")", null); :}
			;

Implements ::= (ImplementsInterfaces) IMPLEMENTS TypeList;
			   
TypeList ::= (Types) TypeList COMMA Type |
			 (FirstType) Type
			 ;

VarDeclList ::= (VarDeclarations) VarDeclList VarDecl |
			 	(NoVarDeclarations) /* Epsilon */
			 	;
			 	
FieldDeclList ::= (FieldDeclarations) FieldDeclList FieldDecl |
			 	  (NoFieldDeclarations) /* Epsilon */
				  ;
			 
ClassMethodDeclBlock ::= (ClassMethodDeclarationsBlock) LCURLY ClassMethodDeclList  RCURLY |
			 		 	 (NoClassMethodDeclarationsBlock) /* Epsilon */
			 		 	 ;

InterfaceDecl ::= (InterfaceDeclDefinition) INTERFACE IntrfaceName LCURLY InterfaceMethodDeclList RCURLY;

IntrfaceName ::= (InterfaceName) IDENT:name;

InterfaceMethodDeclList ::= (InterfaceMethodDeclarations) InterfaceMethodDeclList InterfaceMethodDecl |
						    (NoInterfaceMethodDeclarations) /* Epsilon */
					 	    ;

InterfaceMethodDecl ::= (InterfaceMethodDeclaration) InterfaceMethType InterfaceMethName LPARENT FormParams SEMICOLON;
						
InterfaceMethType ::= (InterfaceMethodType) Type |
					  (NoInterfaceMethodType) VOID
					  ;
					  
InterfaceMethName ::= (InterfaceMethodName) IDENT:name;

MethodDeclList ::= (MethodDeclarations) MethodDeclList MethodDecl |
				   (NoMethodDeclarations) /* Epsilon */
			 	   ;
			 	   
ClassMethodDeclList ::= (ClassMethodDeclarations) ClassMethodDeclList ClassMethodDecl |
					    (NoClassMethodDeclarations) /* Epsilon */
				 	    ;

MethodDecl ::= (MethodDeclaration) MethType MethName LPARENT FormParams VarDeclList MethDeclEnd LCURLY StatementList RCURLY;

MethType ::= (MethodType) Type |
			 (NoMethodType) VOID
			 ;
			 
MethName ::= (MethodName) IDENT:name;

MethDeclEnd ::= (MethodDeclarationEnd) /* Epsilon */;
			   
ClassMethodDecl ::= (ClassMethodDeclaration) ClassMethModifier ClassMethType ClassMethName LPARENT FormParams VarDeclList ClassMethDeclEnd LCURLY StatementList RCURLY;

ClassMethModifier ::= (ClassMethodModifier) Modifier;

ClassMethType ::= (ClassMethodType) Type |
			 	  (NoClassMethodType) VOID
			 	  ;
			 
ClassMethName ::= (ClassMethodName) IDENT:name;

ClassMethDeclEnd ::= (ClassMethodDeclarationEnd) /* Epsilon */;

FormParams ::= (MethodFormalParameters) FormParamList |
			   (NoMethodFormalParameters) RPARENT
			   ;

FormParamList ::= (FormalParameters) FormParam FormParamList |
				  (LastFormalParameter) LastFormParam
			   	  ;

FormParam ::= (ArrayFormalParameter) Type IDENT:name LSQUARE RSQUARE COMMA |
			  (FormalParameter) Type IDENT:name COMMA |
			  (FormalParameterCommaError) error COMMA:c
 	  	 	  {: parser.report_error("Syntax error recovery: Formal parameter[comma] (line: " + cleft + ")", null); :}
			  ;
			  
LastFormParam ::= (LastArrayFormalParameter) Type IDENT:name LSQUARE RSQUARE RPARENT |
			  	  (LastSingleFormalParameter) Type IDENT:name RPARENT |
			  	  (FormalParameterSemicolonError) error RPARENT:r
		 		  {: parser.report_error("Syntax error recovery: Formal parameter[right parenthesis] (line: " + rleft + ")", null); :}
			 	  ;

Type ::= (TypeName) IDENT:name;

StatementList ::= (Statements) StatementList Statement |
				  (NoStatements) /* Epsilon */
			   	  ;

Statement ::= (DesignatorStatement) DesignatorStmt |
			  (IfStatement) IF LPARENT IfCond Statement IfStmtEnd |
			  (IfElseStatement) IF LPARENT IfCond Statement IfStmtEnd ELSE Statement ElseStmtEnd |
			  (ForStatement) FOR LPARENT ForDesignatorStmt ForCond SEMICOLON LastForDesignatorStmt ForDeclEnd Statement |
			  (BreakStatement) BREAK SEMICOLON |
			  (ContinueStatement) CONTINUE SEMICOLON |
			  (TypeReturnStatement) RETURN Expression SEMICOLON |
			  (VoidReturnStatement) RETURN SEMICOLON |
			  (ReadStatement) READ LPARENT Designator RPARENT SEMICOLON |
			  (SimplePrintStatement) PRINT LPARENT Expression RPARENT SEMICOLON |
			  (NumConstPrintStatement) PRINT LPARENT Expression COMMA NUM:value RPARENT SEMICOLON |
			  (BlockStatement) LCURLY StatementList RCURLY
			  ;
			  
IfStmtEnd ::= (IfStatementEnd) /* Epsilon */;

ElseStmtEnd ::= (ElseStatementEnd) /* Epsilon */;

DesignatorStmt ::= (AssignDesignatorStatementWrapper) AssignDesignatorStmt |
				   (FunctionDesignatorStatement) Designator LPARENT ActualParams RPARENT SEMICOLON |
				   (IncDesignatorStatement) Designator INC SEMICOLON |
				   (DecDesignatorStatement) Designator DEC SEMICOLON
				   ;
				   
AssignDesignatorStmt ::= (AssignDesignatorStatement) Designator AssignOp Expression SEMICOLON |
					 	 (AssignError) error SEMICOLON:s
				 		 {: parser.report_error("Syntax error recovery: Assignment (line: " + sleft + ")", null); :}
					 	 ;
					 	 
ForDesignatorStmt ::= (ForDesignatorStatement) DesignatorStmt |
					  (NoForDesignatorStatement) SEMICOLON
			   	 	  ;
			   	 	  
LastForDesignatorStmt ::= (LastForAssignDesignatorStatementWrapper) LastForAssignDesignatorStmt |
				   		  (LastForFunctionDesignatorStatement) Designator LPARENT ActualParams RPARENT RPARENT |
				   		  (LastForIncDesignatorStatement) Designator INC RPARENT |
				   		  (LastForDecDesignatorStatement) Designator DEC RPARENT |
				   		  (NoLastForDesignatorStatement) RPARENT
				   		  ;
				   
LastForAssignDesignatorStmt ::= (LastForAssignDesignatorStatement) Designator AssignOp Expression RPARENT |
							 	(LastForAssignError) error RPARENT:r
						 		{: parser.report_error("Syntax error recovery: Assignment[last in for] (line: " + rleft + ")", null); :}
							 	;
							 	
ForDeclEnd ::= (ForDeclarationEnd) /* Epsilon */;
						
ActualParams ::= (ActualParameters) ActualParamExprList |
			   	 (NoActualParameters) /* Epsilon */
			   	 ;
			   	 
ActualParamExprList ::= (ActualParameterExpressions) ActualParamExprList COMMA Expression |
				   		(FirstActualParameterExpression) Expression
				   		;

Condition ::= (ConditionTerms) Condition OR CondTerm |
			  (FirstConditionTerm) CondTerm
			  ;
			
CondTerm ::= (ConditionFacts) CondTerm AND CondFact |
			 (FirstConditionFact) CondFact
			 ;
			 
CondFact ::= (ConditionExpressions) Expression RelOp Expression |
			 (SimpleConditionExpression) Expression
			 ;

IfCond ::= (IfCondition) Condition RPARENT |
		   (IfConditionError) error RPARENT:r
		   {: parser.report_error("Syntax error recovery: If/Else condition (line: " + rleft + ")", null); :}
		   ;

ForCond ::= (ForCondition) Condition |
			(NoForCondition) /* Epsilon */
			;

Expression ::= (Terms) Expression AddOp Term |
			   (NegFirstTerm) SUB Term |
			   (FirstTerm) Term
			   ;
			   
Term ::= (Factors) Term MulOp Factor |
		 (FirstFactor) Factor
		 ;
		 
Factor ::= (FunctionFactor) Designator LPARENT ActualParams RPARENT |
		   (DesignatorFactor) Designator |
		   (NumFactor) NUM:value |
		   (CharFactor) CHAR:value |
		   (BoolFactor) BOOL:value |
		   (NewArrayFactor) NEW Type LSQUARE Expression RSQUARE |
		   (NewInitializedArrayFactor) NEW Type LSQUARE Expression RSQUARE NewInitArrExprEnd LCURLY InitList RCURLY |
		   (NewFactor) NEW Type |
		   (ExpressionFactor) LPARENT Expression RPARENT
		   ;
		   
NewInitArrExprEnd ::= (NewInitializedArrayExpressionEnd) /* Epsilon */;
		   
InitList ::= (InitializationList) InitExprList |
			 (EmptyInitializationList) /* Epsilon */
			 ;
			 
InitExprList ::= (InitializationExpressions) InitExprList COMMA Expression |
		   		 (FirstInitializationExpression) Expression
		   		 ;

Designator ::= (FieldDesignator) Designator PERIOD IDENT:field |
			   (ArrayDesignator) Designator LSQUARE Expression RSQUARE |
			   (SimpleDesignator) IDENT:name
			   ;

AssignOp ::= (Assign) ASSIGN;

RelOp ::= (Equal) EQU | 
		  (NotEqual) NEQ | 
		  (GreaterThan) GRT | 
		  (GreaterThanEqual) GTE | 
		  (LessThan) LST | 
		  (LessThanEqual) LTE
		  ;

AddOp ::= (Addition) ADD | 
		  (Subtraction) SUB
		  ;

MulOp ::= (Multiplication) MUL | 
		  (Division) DIV | 
		  (Modulus) MOD
		  ;